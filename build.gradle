import org.gradle.api.tasks.testing.logging.TestExceptionFormat

buildscript {
    repositories {
        mavenCentral()

        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.2"
    id "de.undercouch.download" version "3.2.0"
    id 'com.palantir.git-version' version "0.9.1"
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

intellij {
    version ideaVersion
    pluginName 'TYPO3 CMS Plugin'
    plugins = ["com.jetbrains.php:${phpPluginVersion}", "de.sgalinski.typoscript.plugin.id:${typoScriptPluginVersion}", 'CSS', 'java-i18n', 'properties', 'yaml', 'IntelliLang']
    downloadSources !Boolean.valueOf(System.getenv('CI'))

    publishPlugin {
        username System.getenv('IJ_REPO_USERNAME')
        password System.getenv('IJ_REPO_PASSWORD')
    }

    patchPluginXml {
        sinceBuild '173'
        untilBuild '182.*'
    }
}

group 'com.cedricziel'
version gitVersion()

apply plugin: 'de.undercouch.download'
task downloadPsiViewerPlugin() {
    download {
        src 'https://plugins.jetbrains.com/plugin/download?updateId=46431'
        dest new File("${buildDir}/tmp/plugins/", 'PsiViewer.jar')
        onlyIfNewer true
    }
}

task copyPsiViewerPluginToSandBox(type: Copy) {
    from "${buildDir}/tmp/plugins/PsiViewer.jar"
    into "${buildDir}/idea-sandbox/plugins/"
}

copyPsiViewerPluginToSandBox.dependsOn downloadPsiViewerPlugin
copyPsiViewerPluginToSandBox.mustRunAfter prepareSandbox
runIde.dependsOn copyPsiViewerPluginToSandBox

wrapper {
    gradleVersion '4.3.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
