def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

intellij {
    version ideaVersion
    pluginName 'TYPO3 CMS Support'
    plugins = [
        'java',
        'java-i18n',
        'coverage',
        "com.jetbrains.php:${phpPluginVersion}",
        'CSS',
        'properties',
        'yaml',
        "PsiViewer:${psiViewerPluginVersion}",
        project(':lang-fluid'),
        project(':lang-typoscript'),
    ]
}

patchPluginXml {
    if (customSinceBuild) {
        sinceBuild customSinceBuild
    }
    if (customUntilBuild) {
        untilBuild customUntilBuild
    }

    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('src/main/resources/META-INF/description.html')
}

sourceSets {
    test {
        resources {
            srcDir 'testData'
        }
    }
}

test {
    if (ideaVersion.contains("201")) {
        exclude '**/TranslationFoldingBuilderTest_193.class'
    } else if (ideaVersion.contains("193")) {
        exclude '**/TranslationFoldingBuilderTest.class'
    }
}
