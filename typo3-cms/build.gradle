def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

def requiredPlugins = [
    'java',
    'java-i18n',
    'coverage',
    "com.jetbrains.php:${phpPluginVersion}",
    'CSS',
    'JavaScriptLanguage',
    'platform-images',
    'properties',
    'yaml',
    "PsiViewer:${psiViewerPluginVersion}",
    project(':lang-fluid'),
    project(':lang-typoscript'),
]

intellij {
    version ideaVersion
    pluginName 'TYPO3 CMS Support'
    plugins = requiredPlugins
}

patchPluginXml {
    if (customSinceBuild) {
        sinceBuild customSinceBuild
    }
    if (customUntilBuild) {
        untilBuild customUntilBuild
    }

    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('src/main/resources/META-INF/description.html')
}

sourceSets {
    test {
        resources {
            srcDir 'testData'
        }
    }
}
