def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

def requiredPlugins = [
    'java',
    'java-i18n',
    'coverage',
    "com.jetbrains.php:${phpPluginVersion}",
    'CSS',
    'JavaScriptLanguage',
    'properties',
    'yaml',
    "PsiViewer:${psiViewerPluginVersion}",
    project(':lang-fluid'),
    project(':lang-typoscript'),
]

if (ideaVersion.contains("201.") || ideaVersion.contains("2020")) {
    requiredPlugins.add('platform-images')
}

intellij {
    version ideaVersion
    pluginName 'TYPO3 CMS Support'
    plugins = requiredPlugins
}

patchPluginXml {
    if (customSinceBuild) {
        sinceBuild customSinceBuild
    }
    if (customUntilBuild) {
        untilBuild customUntilBuild
    }

    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('src/main/resources/META-INF/description.html')
}

sourceSets {
    test {
        resources {
            srcDir 'testData'
        }
    }
}

test {
    if (ideaVersion.contains("201.") || ideaVersion.contains("2020")) {
        exclude '**/TranslationFoldingBuilderTest_193.class'
    } else if (ideaVersion.contains("193.") || ideaVersion.contains("2019")) {
        exclude '**/TranslationFoldingBuilderTest.class'
    }
}
