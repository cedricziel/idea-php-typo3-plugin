import org.jetbrains.grammarkit.tasks.*

apply plugin: 'org.jetbrains.grammarkit'

def genRoot = file('gen')
sourceSets {
    main {
        java.srcDirs += genRoot
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'test'
        resources.srcDirs 'testData'
    }
}

idea {
    module {
        generatedSourceDirs += genRoot
    }
}

def loadPlugins = [
    "com.jetbrains.php:${phpPluginVersion}",
    'CSS',
    'java',
    'java-i18n',
    'properties',
    "PsiViewer:${psiViewerPluginVersion}"
]
intellij {
    version.set(ideaVersion)
    pluginName.set(pluginNameFluid)
    plugins.set(loadPlugins)
}

def publishChannels = ["nightly"]
publishPlugin {
    channels.set(publishChannels)
    token.set(System.getenv('PUBLISH_TOKEN'))
}

task generateFluidLexer(type: GenerateLexerTask) {
    source = "src/main/grammars/FluidLexer.flex"
    targetDir = "gen/com/cedricziel/idea/fluid/lang/lexer"
    targetClass = "_FluidLexer"
}

task generateFluidParser(type: GenerateParserTask) {
    source = "src/main/grammars/FluidParser.bnf"
    targetRoot = 'gen'
    pathToParser = '/com/cedricziel/idea/fluid/lang/parser/FluidParserGenerated.java'
    pathToPsiRoot = '/com/cedricziel/idea/fluid/lang/psi'
}

compileJava {
    dependsOn generateFluidParser, generateFluidLexer
}

clean.doLast {
    file('gen').deleteDir()
}
